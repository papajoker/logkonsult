pkgname=logkonsult-git
_pkgname="${pkgname%-*}"
pkgver=r8.7672049.20240607
pkgrel=1
arch=("x86_64")
pkgdesc="pacman logs Qt6 gui"
url="https://github.com/papajoker/logkonsult"

depends=(
  python
  pyside6
)
makedepends=(
  python-build
  python-installer
  python-wheel
)

conflicts=("$_pkgname")
provides=("$_pkgname")
replaces=("$_pkgname")

#source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::https://github.com/papajoker/logkonsult/archive/refs/tags/v${pkgver}.tar.gz")
source=("$_pkgname::git+${url}#branch=main")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)" "$(date +%Y%m%d)"
}
# makepkg -fsi -p PKGBUILD.git

build() {
  cd "$_pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  mkdir -p "$pkgdir/usr/share/applications"
  mkdir -p "$pkgdir/usr/share/pixmaps"
  install -Dm644 pkg/logkonsult.desktop "$pkgdir"/usr/share/applications/
  local py_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  py_path="/usr/lib/python$py_version/site-packages/${_pkgname}/assets/${_pkgname}.svg"
  ln -s "$py_path" "$pkgdir/usr/share/pixmaps/"
}


