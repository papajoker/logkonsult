pkgname=logkonsult-git
_pkgname="${pkgname%-*}"
pkgver=0.0.5.r32.g4e7daa7
pkgrel=1
arch=("any")
pkgdesc="pacman logs Qt6 gui"
url="https://github.com/papajoker/logkonsult"

depends=(
  hicolor-icon-theme
  python
  pyside6
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)

conflicts=("$_pkgname")
provides=("$_pkgname")

#source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::https://github.com/papajoker/logkonsult/archive/refs/tags/v${pkgver}.tar.gz")
source=("$_pkgname::git+${url}#branch=main")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}
# makepkg -fsi -p PKGBUILD.git

build() {
  cd "$_pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  mkdir -p "$pkgdir/usr/share/applications"
  mkdir -p "$pkgdir/usr/share/pixmaps"
  install -Dm644 pkg/logkonsult.desktop "$pkgdir"/usr/share/applications/
  local py_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  py_path="/usr/lib/python$py_version/site-packages/${_pkgname}/assets/${_pkgname}.svg"
  ln -s "$py_path" "$pkgdir/usr/share/pixmaps/"
}


