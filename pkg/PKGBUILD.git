pkgname=logkonsult-git
_pkgname="${pkgname%-*}"
pkgver=r64.cf4582c
pkgrel=1
arch=("any")
pkgdesc="pacman logs Qt6 gui"
url="https://github.com/papajoker/logkonsult"

depends=(
  hicolor-icon-theme
  python
  pyside6
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-git-versioning
  python-wheel
)
optdepends=('diffuse: editor, compare merge files'
        'kompare: editor, compare merge files'
        'meld: editor, compare merge files'
        'tkdiff: a graphical front end to the diff program'
        'gvfs: Virtual filesystem implementation for GIO'
        'kio-admin: Manage files as administrator using the admin:// KIO protocol'
        'code: Visual Studio Code'
        'geany: Fast and lightweight IDE'
        'gedit: GNOME Text Editor'
        'gnome-text-editor: A simple text editor for the GNOME desktop'
        'kate: kde Advanced text editor'
        'pluma: A powerful text editor for MATE'
        'mousepad: Simple text editor for Xfce'
        'zed: A high-performance, multiplayer code editor')

conflicts=("$_pkgname")
provides=("$_pkgname")
# install=.install
source=("$_pkgname::git+${url}#branch=main")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}
# makepkg -fsi -p PKGBUILD.git

build() {
  cd "$_pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$_pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -d "$pkgdir/usr/share/applications"
  install -d "$pkgdir/usr/share/icons/hicolor/scalable/apps"
  install -Dm644 pkg/logkonsult.desktop "$pkgdir"/usr/share/applications/
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  ln -s "${site_packages}/${_pkgname}/assets/${_pkgname}.svg" \
    "$pkgdir/usr/share/icons/hicolor/scalable/apps/"
}


